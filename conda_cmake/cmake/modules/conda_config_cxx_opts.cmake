
include(conda_generated_copts)
include(conda_print_list)

set(CONDA_LSAN_LINKOPTS "")
set(CONDA_HAVE_LSAN OFF)
set(CONDA_DEFAULT_LINKOPTS "")

if (BUILD_SHARED_LIBS AND MSVC)
    set(CONDA_BUILD_DLL TRUE)
    set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
else()
    set(CONDA_BUILD_DLL FALSE)
endif()

if("${CMAKE_SYSTEM_PROCESSOR}" STREQUAL "x86_64" OR "${CMAKE_SYSTEM_PROCESSOR}" STREQUAL "AMD64")
    if (MSVC)
        set(CONDA_RANDOM_RANDEN_COPTS "${CONDA_RANDOM_HWAES_MSVC_X64_FLAGS}")
    else()
        set(CONDA_RANDOM_RANDEN_COPTS "${CONDA_RANDOM_HWAES_X64_FLAGS}")
    endif()
elseif("${CMAKE_SYSTEM_PROCESSOR}" MATCHES "arm.*|aarch64")
    if ("${CMAKE_SIZEOF_VOID_P}" STREQUAL "8")
        set(CONDA_RANDOM_RANDEN_COPTS "${CONDA_RANDOM_HWAES_ARM64_FLAGS}")
    elseif("${CMAKE_SIZEOF_VOID_P}" STREQUAL "4")
        set(CONDA_RANDOM_RANDEN_COPTS "${CONDA_RANDOM_HWAES_ARM32_FLAGS}")
    else()
        message(WARNING "Value of CMAKE_SIZEOF_VOID_P (${CMAKE_SIZEOF_VOID_P}) is not supported.")
    endif()
else()
    message(WARNING "Value of CMAKE_SYSTEM_PROCESSOR (${CMAKE_SYSTEM_PROCESSOR}) is unknown and cannot be used to set CONDA_RANDOM_RANDEN_COPTS")
    set(CONDA_RANDOM_RANDEN_COPTS "")
endif()


if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
    set(CONDA_DEFAULT_COPTS "${CONDA_GCC_FLAGS}")
    set(CONDA_TEST_COPTS "${CONDA_GCC_FLAGS};${CONDA_GCC_TEST_FLAGS}")
elseif("${CMAKE_CXX_COMPILER_ID}" MATCHES "Clang")
    # MATCHES so we get both Clang and AppleClang
    if(MSVC)
        # clang-cl is half MSVC, half LLVM
        set(CONDA_DEFAULT_COPTS "${CONDA_CLANG_CL_FLAGS}")
        set(CONDA_TEST_COPTS "${CONDA_CLANG_CL_FLAGS};${CONDA_CLANG_CL_TEST_FLAGS}")
        set(CONDA_DEFAULT_LINKOPTS "${CONDA_MSVC_LINKOPTS}")
    else()
        set(CONDA_DEFAULT_COPTS "${CONDA_LLVM_FLAGS}")
        set(CONDA_TEST_COPTS "${CONDA_LLVM_FLAGS};${CONDA_LLVM_TEST_FLAGS}")
        if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
            # AppleClang doesn't have lsan
            # https://developer.apple.com/documentation/code_diagnostics
            if(NOT CMAKE_CXX_COMPILER_VERSION VERSION_LESS 3.5)
                set(CONDA_LSAN_LINKOPTS "-fsanitize=leak")
                set(CONDA_HAVE_LSAN ON)
            endif()
        endif()
    endif()
elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")
    set(CONDA_DEFAULT_COPTS "${CONDA_MSVC_FLAGS}")
    set(CONDA_TEST_COPTS "${CONDA_MSVC_FLAGS};${CONDA_MSVC_TEST_FLAGS}")
    set(CONDA_DEFAULT_LINKOPTS "${CONDA_MSVC_LINKOPTS}")
else()
    message(WARNING "Unknown compiler: ${CMAKE_CXX_COMPILER}.  Building with no default flags")
    set(CONDA_DEFAULT_COPTS "")
    set(CONDA_TEST_COPTS "")
endif()

set(CONDA_CXX_STANDARD "${CMAKE_CXX_STANDARD}")

